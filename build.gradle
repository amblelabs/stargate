plugins {
	id "fabric-loom" version "$loom_version"
	id "maven-publish"
}

apply from: "versioning.gradle"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		url "https://theo.is-a.dev/maven/"

		content {
			includeGroup "dev.drtheo"
		}
	}

	maven {
		url "https://amblelabs.github.io/maven"

		content {
			includeGroup "dev.amble"
		}
	}

	maven {
		url "https://dl.cloudsmith.io/public/klikli-dev/mods/maven/"
		content {
			includeGroup "com.klikli_dev"
		}
	}

	maven {
		name = "Jei and Patchouli Maven"
		url = 'https://maven.blamejared.com'
		content {
			includeGroup "vazkii.patchouli"
			includeGroup "mezz.jei"
		}
	}
}

loom {
	// accessWidenerPath = file("src/main/resources/${project.mod_id}.accesswidener")

	splitEnvironmentSourceSets()

	mods {
		"${project.mod_id}" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

fabricApi {
	configureDataGeneration() {
		client = true
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi("dev.amble:lib:${project.amblekit_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modCompileOnlyApi "teamreborn:energy:${project.tech_version}"
	modImplementation "com.klikli_dev:modonomicon-${minecraft_version}-fabric:${modonomicon_version}"
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	inputs.property "version", project.version
	inputs.property "minecraft_version", project.minecraft_version
	inputs.property "loader_version", project.loader_version
	inputs.property "amblekit_version", project.amblekit_version

	exclude '.cache'

	def expansionVars = [
			"version": project.version,
			"minecraft_version": project.minecraft_version,
			"loader_version": project.loader_version,
			"amblekit_version": project.amblekit_version
	]

	filesMatching("fabric.mod.json") {
		expand expansionVars
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_22
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties["archivesName"]}" }
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			version = artifactVersion
			from components.java
		}
	}

	repositories {
		maven {
			name = "ci"
			url = providers.environmentVariable("CI_MAVEN_PATH")
		}
	}
}
